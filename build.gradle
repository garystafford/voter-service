buildscript {
    ext {
        springBootVersion = '1.4.3.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.updateimpact:updateimpact-gradle-plugin:1.1.6'
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.updateimpact.updateimpact'

springBoot {
    executable = true
}

def application_name = 'voter-service'
def major = '0.2'
def minor = System.env.TRAVIS_BUILD_NUMBER ?: '0'
def artifact_version = major + '.' + minor

jar {
    baseName = application_name
    version = artifact_version
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-actuator-docs')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.webjars:hal-browser')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR3'
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

updateImpactSubmit {
    apiKey = System.env.updateimpactApiKey
    openBrowser = true
}
